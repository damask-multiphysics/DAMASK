foreach(VARIANT GRID MESH TEST)
  if(${VARIANT})
    string(TOLOWER "${VARIANT}" VARIANT_LOWER)
    set(executable_name "DAMASK_${VARIANT_LOWER}")

    file(GLOB damask_fortran CONFIGURE_DEPENDS *.f90 ${VARIANT_LOWER}/*.f90)
    file(GLOB damask_c CONFIGURE_DEPENDS *.c *.cpp ${VARIANT_LOWER}/*.c ${VARIANT_LOWER}/*.cpp)

    if(NOT CMAKE_BUILD_TYPE STREQUAL "SYNTAXONLY")
      add_library(damask_fortran_${VARIANT} OBJECT ${damask_fortran})
      target_compile_definitions(damask_fortran_${VARIANT} PUBLIC ${VARIANT})
      set_target_properties(damask_fortran_${VARIANT}
        PROPERTIES Fortran_MODULE_DIRECTORY
          ${CMAKE_CURRENT_BINARY_DIR}/${VARIANT}_${CMAKE_BUILD_TYPE})
      add_executable(${executable_name} $<TARGET_OBJECTS:damask_fortran_${VARIANT}>)
      set_target_properties(${executable_name} PROPERTIES LINKER_LANGUAGE Fortran)

      add_library(damask_c_${VARIANT} STATIC ${damask_c})
      target_compile_definitions(damask_c_${VARIANT} PUBLIC ${VARIANT})
      if(Boost_FOUND)
        target_link_libraries(damask_c_${VARIANT} PUBLIC Boost::program_options)
        target_compile_definitions(damask_c_${VARIANT} PUBLIC BOOST)
        add_compile_definitions(BOOST)
      endif()
      target_link_libraries(${executable_name} PRIVATE damask_c_${VARIANT})
      install(TARGETS ${executable_name} RUNTIME DESTINATION bin)
    else()
      add_library(${executable_name} OBJECT ${damask_fortran} ${damask_c})
      exec_program(mktemp OUTPUT_VARIABLE nothing)
      exec_program(mktemp ARGS -d OUTPUT_VARIABLE black_hole)
      install(PROGRAMS ${nothing} DESTINATION ${black_hole})
    endif()
  endif()
  string(REPLACE ";" "\n" sources_${VARIANT} "${damask_fortran};${damask_c};${damask_c_${VARIANT}}")
  file(WRITE ${CMAKE_BINARY_DIR}/sources_${VARIANT}.txt ${sources_${VARIANT}})
endforeach()
